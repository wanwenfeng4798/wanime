allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://maven.aliyun.com/repository/public" }
    }
}

rootProject.layout.buildDirectory.value(rootProject.layout.buildDirectory.dir("../../build").get())
subprojects {
    afterEvaluate { project ->
        if (project.plugins.hasPlugin("com.android.application") || project.plugins.hasPlugin("com.android.library")) {
            project.android {
                compileSdkVersion 35
                buildToolsVersion "35.0.0"
                if (namespace == null) {
                    namespace project.group
                }
            }
        }
    }
//    rootProject.layout.buildDirectory.value("${rootProject.layout.buildDirectory}/${project.name}")
    project.layout.buildDirectory.value(rootProject.layout.buildDirectory.dir(project.name).get())
    project.evaluationDependsOn(':app')
    dependencyLocking {
        ignoredDependencies.add("io.flutter:*")
        lockFile = file("${rootProject.projectDir}/project-${project.name}.lockfile")
        if (!project.hasProperty("local-engine-repo")) {
            lockAllConfigurations()
        }
    }
}

tasks.register("clean", Delete) {
    delete(rootProject.layout.buildDirectory)
}
